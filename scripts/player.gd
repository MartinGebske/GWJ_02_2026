extends CharacterBody3D


@export var mouse_sensitivity: float = 0.00075
@export var min_boundary: float = -60.0
@export var max_boundary: float = 10.0


const SPEED = 5.0
const JUMP_VELOCITY = 9.0

var _look := Vector2.ZERO
var respawn_position: Vector3 = Vector3.ZERO
var is_movement_blocked: bool = false
var movement_block_timer: float = 0.5
var current_block_time: float = 0.0

@onready var horizontal_pivot: Node3D = $HorizontalPivot
@onready var vertical_pivot: Node3D = $HorizontalPivot/VerticalPivot
@onready var deadzone: Area3D = $"../Deadzone"


func _ready() -> void:
	Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	respawn_position = position
	deadzone.player_fell.connect(_on_respawn)

func _physics_process(delta: float) -> void:
	frame_camera_rotation()
	if is_movement_blocked:
		current_block_time -= delta
		if current_block_time <= 0:
			is_movement_blocked = false
		return

	# Add the gravity.
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Handle jump.
	if Input.is_action_just_pressed("ui_accept") and is_on_floor():
		velocity.y = JUMP_VELOCITY


	var direction := get_movement_direction()
	if direction:
		velocity.x = direction.x * SPEED
		velocity.z = direction.z * SPEED
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		velocity.z = move_toward(velocity.z, 0, SPEED)

	move_and_slide()

func _unhandled_input(event: InputEvent) -> void:
	if event.is_action_pressed("ui_cancel"):
		Input.mouse_mode = Input.MOUSE_MODE_VISIBLE
	if Input.mouse_mode == Input.MOUSE_MODE_CAPTURED:
		if event is InputEventMouseMotion:
			_look += -event.relative * mouse_sensitivity

func get_movement_direction() -> Vector3:
	var input_dir := Input.get_vector("ui_left", "ui_right", "ui_up", "ui_down")
	var input_vector := Vector3(input_dir.x, 0, input_dir.y).normalized()
	return horizontal_pivot.global_transform.basis * input_vector

func frame_camera_rotation() -> void:
	horizontal_pivot.rotate_y(_look.x)
	vertical_pivot.rotate_x(_look.y)

	vertical_pivot.rotation.x = clampf(vertical_pivot.rotation.x,
										deg_to_rad(min_boundary),
										deg_to_rad(max_boundary)
										)
	_look = Vector2.ZERO

func _on_respawn() -> void:
	position = respawn_position
	velocity = Vector3.ZERO
	is_movement_blocked = true
	current_block_time = movement_block_timer
